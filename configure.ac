AC_INIT(mail-notification, 0.8.1, jylefort@brutele.be)
AC_CONFIG_SRCDIR(src/mn-main.c)

AC_PREREQ(2.59)

### command line arguments

MN_ARG_ENABLE(mbox, [disable mbox support])
MN_ARG_ENABLE(mh, [disable MH support])
MN_ARG_ENABLE(maildir, [disable Maildir support])
MN_ARG_ENABLE(pop3, [disable POP3 support])
MN_ARG_ENABLE(imap, [disable IMAP support])
MN_ARG_ENABLE(ssl, [disable SSL/TLS support])
MN_ARG_ENABLE(sasl, [disable SASL authentication support])
MN_ARG_ENABLE(sylpheed, [disable Sylpheed support])
MN_ARG_ENABLE(gmail, [disable Gmail support])
MN_ARG_ENABLE(ipv6, [disable IPv6 support])
MN_ARG_ENABLE(mime, [disable MIME support])

MN_ARG_COMPILE_WARNINGS
MN_ARG_ENABLE(regression-tests, [build and run regression tests], no)

### initialization

AM_INIT_AUTOMAKE(foreign)
AM_MAINTAINER_MODE
AM_CONFIG_HEADER(config.h)

### i18n

GETTEXT_PACKAGE=AC_PACKAGE_NAME
AC_DEFINE_UNQUOTED(GETTEXT_PACKAGE, "$GETTEXT_PACKAGE", [Define to the gettext package name])
AC_SUBST(GETTEXT_PACKAGE)

ALL_LINGUAS="bg de fr pt ru sr sr@Latn"
AM_GLIB_GNU_GETTEXT

AC_PROG_INTLTOOL

### system features

AC_PROG_CC
AC_SYS_LARGEFILE

# check for ANSI C headers
AC_HEADER_STDC
if test $ac_cv_header_stdc != yes; then
	AC_MSG_ERROR([ANSI C headers not present])
fi

# timegm() is non-standard, define HAVE_TIMEGM if it is present
AC_CHECK_FUNCS(timegm)

# see if "struct tm" is defined in sys/time.h
AC_STRUCT_TM

### mandatory libraries and programs

AM_PATH_GTK_2_0(2.4.0,, [AC_MSG_ERROR([unable to find the GTK+ library])])

PKG_CHECK_MODULES(GNOME, [gthread-2.0 gconf-2.0 >= 2.4.0 libgnomeui-2.0 gnome-vfs-2.0 libglade-2.0 eel-2.0 >= 2.6.0 bonobo-activation-2.0],, [AC_MSG_ERROR([unable to find the GNOME libraries])])
AM_GCONF_SOURCE_2

AC_PATH_PROG(GCONFTOOL, gconftool-2)
if test -z "$GCONFTOOL"; then
	AC_MSG_ERROR([unable to find the gconftool-2 program])
fi

ORBIT_IDL="`$PKG_CONFIG --variable=orbit_idl ORBit-2.0`"
AC_SUBST(ORBIT_IDL)

LIBBONOBO_IDL="`$PKG_CONFIG --variable=idldir libbonobo-2.0`"
AC_SUBST(LIBBONOBO_IDL)

BONOBO_ACTIVATION_IDL="`$PKG_CONFIG --variable=idldir bonobo-activation-2.0`"
AC_SUBST(BONOBO_ACTIVATION_IDL)

### optional libraries

# not needed by end-users, as files generated by GOB are distributed
AC_PATH_PROG(GOB2, gob2)

if MN_FEATURE_DISABLED(pop3) && MN_FEATURE_DISABLED(imap); then
	if MN_FEATURE_ENABLED(ssl); then
		MN_FEATURE_DISABLE(ssl, [POP3 and IMAP support disabled])
	fi
	if MN_FEATURE_ENABLED(sasl); then
		MN_FEATURE_DISABLE(sasl, [POP3 and IMAP support disabled])
	fi
	if MN_FEATURE_ENABLED(ipv6); then
		MN_FEATURE_DISABLE(ipv6, [POP3 and IMAP support disabled])
	fi
fi

if MN_FEATURE_ENABLED(ssl); then
	AM_PATH_OPENSSL(,, [MN_FEATURE_DISABLE(ssl, [OpenSSL not found])])
fi
if MN_FEATURE_ENABLED(sasl); then
	AM_PATH_SASL2(,, [MN_FEATURE_DISABLE(sasl, [Cyrus SASL not found])])
fi

if MN_FEATURE_ENABLED(gmail); then
	PKG_CHECK_MODULES(SOUP, libsoup-2.2,, [MN_FEATURE_DISABLE(gmail, [libsoup not found])])
fi	

if MN_FEATURE_ENABLED(mime) &&
   MN_FEATURE_DISABLED(mbox) &&
   MN_FEATURE_DISABLED(mh) &&
   MN_FEATURE_DISABLED(maildir) &&
   MN_FEATURE_DISABLED(pop3) &&
   MN_FEATURE_DISABLED(imap) &&
   MN_FEATURE_DISABLED(sylpheed); then
	MN_FEATURE_DISABLE(mime, [mbox, MH, Maildir, POP3, IMAP and Sylpheed support disabled])
fi

if MN_FEATURE_ENABLED(mime); then
	PKG_CHECK_MODULES(GMIME, [gmime-2.0 >= 2.1.0],, [MN_FEATURE_DISABLE(mime, [GMime not found])])
fi

### Automake conditionals

AM_CONDITIONAL(WITH_MBOX, [MN_FEATURE_ENABLED(mbox)])
AM_CONDITIONAL(WITH_MH, [MN_FEATURE_ENABLED(mh)])
AM_CONDITIONAL(WITH_MAILDIR, [MN_FEATURE_ENABLED(maildir)])
AM_CONDITIONAL(WITH_POP3, [MN_FEATURE_ENABLED(pop3)])
AM_CONDITIONAL(WITH_IMAP, [MN_FEATURE_ENABLED(imap)])
AM_CONDITIONAL(WITH_POP3_OR_IMAP, [MN_FEATURE_ENABLED(pop3) || MN_FEATURE_ENABLED(imap)])
AM_CONDITIONAL(WITH_POP3_OR_IMAP_OR_GMAIL, [MN_FEATURE_ENABLED(pop3) || MN_FEATURE_ENABLED(imap) || MN_FEATURE_ENABLED(gmail)])
AM_CONDITIONAL(WITH_SSL, [MN_FEATURE_ENABLED(ssl)])
AM_CONDITIONAL(WITH_SASL, [MN_FEATURE_ENABLED(sasl)])
AM_CONDITIONAL(WITH_SYLPHEED, [MN_FEATURE_ENABLED(sylpheed)])
AM_CONDITIONAL(WITH_GMAIL, [MN_FEATURE_ENABLED(gmail)])
AM_CONDITIONAL(WITH_MIME, [MN_FEATURE_ENABLED(mime)])
AM_CONDITIONAL(WITH_REGRESSION_TESTS, [MN_FEATURE_ENABLED(regression-tests)])

### config.h definitions

MN_FEATURES_DEFINE([mbox, mh, maildir, pop3, imap, ssl, sasl, sylpheed, gmail, ipv6, mime])

### output

AC_CONFIG_FILES(Makefile
		art/Makefile
		data/Makefile
		help/Makefile
		help/C/Makefile
		m4/Makefile 
		po/Makefile.in
		src/Makefile
		tests/Makefile
		ui/Makefile)
AC_OUTPUT

### report

cat <<EOF

$PACKAGE $VERSION is ready to be built.
The following settings will be used:

Installation prefixes
MN_REPORT_ARGS([prefix, exec-prefix])
Installation directories
MN_REPORT_ARGS([bindir, datadir, sysconfdir, libdir])
Features
MN_REPORT_FEATURES([mbox, mh, maildir, pop3, imap, ssl, sasl, sylpheed, gmail, ipv6, mime])
Type "make" to build $PACKAGE $VERSION.
EOF
